#General ACF settings

CONFIG += stl
CONFIG -= exceptions

COMPILER_NAME = QMake
win32-msvc*{
	COMPILER_NAME = VC

	win32-msvc2005{
		COMPILER_NAME = VC8
	}
	win32-msvc2008{
		COMPILER_NAME = VC9
	}
	win32-msvc2010{
		COMPILER_NAME = VC10
	}
}

CONFIG(debug, debug|release){
	COMPILER_DIR = Debug$$COMPILER_NAME
}
CONFIG(release, debug|release){
	COMPILER_DIR = Release$$COMPILER_NAME
}

!win32-msvc*{
	QMAKE_LFLAGS -= -mthreads
	QMAKE_LFLAGS += -fvisibility=hidden
}

INCLUDEPATH += $$PWD/../../Include
LIBS += -L$$PWD/../../Lib/$$COMPILER_DIR

ARXC_CONFIG = ../../../Config/Full.xpc

# custom build for ACF Registry Compiler (Arxc)
arxCompiler.name = ACF-Components
arxCompiler.CONFIG += target_predeps
arxCompiler.output = ${QMAKE_FILE_IN_PATH}/Generated/C${QMAKE_FILE_BASE}.cpp
arxCompiler.commands = $$PWD/../../Bin/$$COMPILER_DIR/Arxc.exe ${QMAKE_FILE_IN} -o ${QMAKE_FILE_IN_PATH}/Generated/C${QMAKE_FILE_BASE}.cpp -config $${ARXC_CONFIG}
win32*:arxCompiler.clean = ${QMAKE_FILE_IN_PATH}/Generated/C${QMAKE_FILE_BASE}.cpp ${QMAKE_FILE_IN_PATH}/Generated/C${QMAKE_FILE_BASE}.h
arxCompiler.depends = $$PWD/../../Bin/$$COMPILER_DIR/Arxc.exe
arxCompiler.input = ARXC_FILES
arxCompiler.variable_out = SOURCES
QMAKE_EXTRA_COMPILERS += arxCompiler

